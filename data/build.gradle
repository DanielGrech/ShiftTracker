apply plugin: 'com.android.library'


apply from: '../buildscripts/jacoco.gradle'

android {
    compileSdkVersion config.compileSdkVersion
    buildToolsVersion config.buildToolsVersion

    defaultConfig {
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode config.versionCode
        versionName config.versionName
    }

    publishNonDefault true

    buildTypes {

        jacocoEnabled.initWith(buildTypes.debug)
        jacocoEnabled {
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
        }
    }

    productFlavors {
        paid {
            buildConfigField "String", "CONTENT_PROVIDER_AUTHORITY", "\"${config.contentProviderAuthority}\""
            manifestPlaceholders = [
                    "contentProviderAuthority" : "${config.contentProviderAuthority}"
            ]
        }

        free {
            buildConfigField "String", "CONTENT_PROVIDER_AUTHORITY", "\"${config.contentProviderAuthorityFree}\""
            manifestPlaceholders = [
                    "contentProviderAuthority" : "${config.contentProviderAuthorityFree}"
            ]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    libraryVariants.matching { it.buildType.name == 'jacocoEnabled' }.all { createJacocoTask(it) }
}

dependencies {
    compile project(':model')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.squareup.sqlbrite:sqlbrite:${versions.sqlbrite}"
    compile "io.reactivex:rxjava:${versions.rxJava}"

    testCompile "junit:junit:${versions.junit}"
    testCompile "org.mockito:mockito-core:${versions.mockito}"
}
